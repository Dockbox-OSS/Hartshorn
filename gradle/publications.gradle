apply plugin: 'maven-publish'
apply plugin: 'signing'

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                afterEvaluate {
                    name = project.description
                    description = project.description
                    url = "https://github.com/GuusLieben"
                }

                organization {
                    name = "Hartshorn"
                    url = "https://github.com/GuusLieben"
                }
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://github.com/GuusLieben/Hartshorn/blob/develop/LICENSE'
                        distribution = "repo"
                    }
                }
                scm {
                    url = 'https://www.github.com/GuusLieben/Hartshorn'
                    connection = 'scm:git:git://github.com/GuusLieben/Hartshorn'
                    developerConnection = 'scm:git:git://github.com/GuusLieben/Hartshorn'
                }
                developers {
                    developer {
                        id = "guuslieben"
                        name = "Guus Lieben"
                        email = "guuslieben@xendox.com"
                    }
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://www.github.com/GuusLieben/Hartshorn/issues"
                }
            }
            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        repositories {
            maven {
                name = "ossrh"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.findProperty('ossrh.username')
                    password = project.findProperty('ossrh.password')
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

task copyPublications {
    doLast {
        final def version = projectVersion
        final def DIST_DEST_DIR = "$rootDir/hartshorn-assembly/publications/$version"
        final def DIRT_SRC_DIR = "$buildDir/libs"
        final def POM_SRC_DIR = "$buildDir/publications/mavenJava"

        ant.mkdir(dir: DIST_DEST_DIR)
        ant.copy(todir: DIST_DEST_DIR) {
            fileset dir: DIRT_SRC_DIR,
                    includes: "$project.name-$version*"
        }

        file("$POM_SRC_DIR/pom-default.xml").renameTo(file("$POM_SRC_DIR/${project.name}.pom"))
        file("$POM_SRC_DIR/pom-default.xml.asc").renameTo(file("$POM_SRC_DIR/${project.name}.pom.asc"))

        ant.copy(todir: DIST_DEST_DIR) {
            fileset dir: POM_SRC_DIR,
                    includes: "${project.name}.pom**"
        }

    }
}

publishToMavenLocal.dependsOn generatePomFileForMavenJavaPublication
publishToMavenLocal.finalizedBy copyPublications
