import java.time.LocalDate

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

def getVersionName = { ->
    return 'git rev-parse --verify --short HEAD'.execute().text.trim()
}

version = getVersionName()
group = project.pluginGroup
description = 'Common sources for Darwin Server plugins'

subprojects { Project subProject ->
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = project.pluginGroup
    version = getVersionName()

    task copyArtifacts {
        doLast {
            def version = getVersionName()
            def date = LocalDate.now().toString();
            def DIST_DIR = "${rootDir}/dist/${date}/${version}"
            def SRC_DIR = "${buildDir}/libs"

            ant.mkdir(dir: DIST_DIR)
            ant.copy(todir: DIST_DIR) {
                fileset dir: SRC_DIR,
                        includes: "*.jar"
            }
            delete buildDir
        }
    }

    afterEvaluate {
        def buildTask = subProject.tasks.find { it.name == 'build' }
        if (buildTask) {
            buildTask.finalizedBy(copyArtifacts)
        }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        maven {
            name 'fastasyncworldedit'
            url 'https://ci.athion.net/job/FastAsyncWorldEdit/ws/mvn'
        }
        maven {
            name 'jitpack'
            url 'https://jitpack.io'
        }
        maven {
            name 'sk89q'
            url 'https://maven.sk89q.com/repo/'
        }
        mavenCentral()
        jcenter()
    }

    tasks.build.dependsOn tasks.shadowJar
}
