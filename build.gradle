import java.time.LocalDate

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

def getVersionName = { ->
    return 'git rev-parse --verify --short HEAD'.execute().text.trim()
}

version = getVersionName()
group = project.pluginGroup
description = 'Common sources for Darwin Server plugins'

subprojects { Project subProject ->
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = project.pluginGroup
    version = getVersionName()
    def baseName = subProject.archivesBaseName
    jar.baseName = "DarwinServer-${baseName}"

    task copyArtifacts {
        doLast {
            def version = getVersionName()
            def date = LocalDate.now().toString();
            def DIST_DIR = "${rootDir}/dist/${date}/${version}"
            def SRC_DIR = "${buildDir}/libs"

            ant.mkdir(dir: DIST_DIR)
            ant.copy(todir: DIST_DIR) {
                fileset(dir: SRC_DIR,
                        includes: "DarwinServer-${baseName}*${version}*.jar")
            }
            delete buildDir
        }
    }

    afterEvaluate {
        def buildTask = subProject.tasks.find { it.name == 'build' }
        if (buildTask) {
            buildTask.finalizedBy(copyArtifacts)
        }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    repositories {
        maven {
            name 'sk89q'
            url 'https://maven.sk89q.com/repo/'
        }
    }

    dependencies {
        compile fileTree(dir: 'common_libs', include: ['*.jar'])
    }
}
