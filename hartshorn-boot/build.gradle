import java.time.LocalDate

/*
 *  Copyright (C) 2020 Guus Lieben
 *
 *  This framework is free software; you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as
 *  published by the Free Software Foundation, either version 2.1 of the
 *  License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See
 *  the GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with this library. If not, see {@literal<http://www.gnu.org/licenses/>}.
 */

plugins {
    id 'base'
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

archivesBaseName = 'hartshorn-boot'
description = 'Application boot for Hartshorn'
def date = LocalDate.now().toString()

repositories {
    mavenCentral()
}

configurations {
    includeShadow
    include.extendsFrom(includeShadow)
    shadow.extendsFrom(includeShadow)
}

def ignoreProjects = ['examples', 'hartshorn-boot']

dependencies {
    rootDir.listFiles().each {
        if ((!ignoreProjects.contains(it.name)) && it.directory && new File(it, 'build.gradle').exists()) {
            includeShadow "org.dockbox.hartshorn:${it.name}"
            includeShadow project(":${it.name}").sourceSets.impl.output
        }
    }

    // Additional shaded dependencies, these are not available on the platform by default
    includeShadow "org.hibernate:hibernate-core:$hibernateVersion"
    includeShadow "org.apache.derby:derby:$derbyVersion"
    includeShadow "javax.xml.bind:jaxb-api:$javaxJaxbVersion"

    includeShadow "org.apache.logging.log4j:log4j-api:$log4jVersion"
    includeShadow "org.apache.logging.log4j:log4j-core:$log4jVersion"
    includeShadow "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}

tasks.build {
    dependsOn tasks.shadowJar
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'com.fasterxml.jackson', 'org.dockbox.hartshorn.internal.jackson'
    relocate 'org.apache.commons', 'org.dockbox.hartshorn.internal.apache.commons'

    dependencies {
        exclude(dependency('org.projectlombok:lombok'))
        exclude(dependency('org.slf4j:slf4j-api'))
    }
}

build.finalizedBy copyArtifacts
